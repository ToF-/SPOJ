\ -------- input.fs ------------

REQUIRE hash-table.fs
REQUIRE parse.fs
REQUIRE request.fs

256 CONSTANT LINE-MAX

CREATE LINE-BUFFER LINE-MAX ALLOT

VARIABLE INPUT-FILE

: OPEN-INPUT-FILE ( addr,count -- )
    R/O OPEN-FILE THROW INPUT-FILE ! ;

: CLOSE-INPUT-FILE
    INPUT-FILE @ CLOSE-FILE THROW ;

: READ-INPUT-LINE ( -- addr,count,flag )
    LINE-BUFFER DUP LINE-MAX
    INPUT-FILE @ READ-LINE THROW ;

: READ-EDGES ( n -- )
    0 DO
        READ-INPUT-LINE
        STR-TOKENS ASSERT( 2 = )
        STR>NUMBER 1- -ROT STR>NUMBER 1-
        SWAP I LAST-VERTEX SET-EDGE
    LOOP ;

: READ-VERTEX
    READ-INPUT-LINE
    STR-TOKENS ASSERT( 1 = )
    STR>NUMBER DUP
    >R INSERT-VERTEX
    R> READ-EDGES ;

: READ-VERTICE
    READ-INPUT-LINE
    STR-TOKENS ASSERT( 1 = )
    STR>NUMBER 0 DO READ-VERTEX LOOP ;

: READ-REQUEST
    READ-INPUT-LINE
    STR-TOKENS ASSERT( 2 = )
    2SWAP FIND-VERTEX -ROT FIND-VERTEX
    ADD-REQUEST ;

: READ-REQUESTS
    REQUESTS OFF
    READ-INPUT-LINE
    STR-TOKENS ASSERT( 1 = )
    STR>NUMBER 0 DO READ-REQUEST LOOP ;



