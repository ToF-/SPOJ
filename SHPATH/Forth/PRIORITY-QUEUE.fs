REQUIRE HEAP-MEMORY.fs

10000 CONSTANT /PRIOR-QUEUE

H-CREATE PRIOR-QUEUE /PRIOR-QUEUE 1+ CELLS 2* H-ALLOT
H-CREATE QUEUE-INDEX /PRIOR-QUEUE 1+ CELLS H-ALLOT


: PRIOR-QUEUE-INIT
    PRIOR-QUEUE /PRIOR-QUEUE CELLS 2* CELL+ ERASE
    QUEUE-INDEX /PRIOR-QUEUE 1+ CELLS ERASE ;


: PRIOR-QUEUE-ITEM-ADDRESS ( i -- addr )
    CELLS 2* PRIOR-QUEUE + ;

: PRIOR-QUEUE-COMPARE ( i,j -- n )
    SWAP PRIOR-QUEUE-ITEM-ADDRESS @
    SWAP PRIOR-QUEUE-ITEM-ADDRESS @ - ;

: QUEUE-INDEX-SWAP ( i,j -- )
    2DUP PRIOR-QUEUE-ITEM-ADDRESS 2@ DROP   \ i,j,i,index[j]
    SWAP PRIOR-QUEUE-ITEM-ADDRESS 2@ DROP   \ i,j,index[j],index[i]
    CELLS QUEUE-INDEX + ROT SWAP ! SWAP ! ;


: PRIOR-QUEUE-SWAP ( i,j -- )
    \ 2DUP QUEUE-INDEX-SWAP
    SWAP PRIOR-QUEUE-ITEM-ADDRESS
    SWAP PRIOR-QUEUE-ITEM-ADDRESS
    OVER 2@ 2>R TUCK 2@ ROT 2! 2R> ROT 2! ;

: PRIOR-QUEUE-SIFT-UP ( index -- )
    BEGIN
        DUP 1 > WHILE
        DUP 2/
        2DUP PRIOR-QUEUE-COMPARE 0< IF
            2DUP PRIOR-QUEUE-SWAP
            NIP
        ELSE
            2DROP 0
        THEN
    REPEAT DROP ;

: PRIOR-QUEUE-SIFT-DOWN ( index -- )
    BEGIN
        DUP 2*
        DUP PRIOR-QUEUE @ <= WHILE
        DUP PRIOR-QUEUE @ < IF
            DUP 1+ 2DUP PRIOR-QUEUE-COMPARE
            0< IF DROP ELSE NIP THEN
        THEN
        2DUP PRIOR-QUEUE-COMPARE 0> IF
            2DUP PRIOR-QUEUE-SWAP
            NIP
        ELSE
            DROP PRIOR-QUEUE @
        THEN
    REPEAT 2DROP ;

: PRIOR-QUEUE-INSERT ( record, priority -- )
    ASSERT( PRIOR-QUEUE @ /PRIOR-QUEUE <= )
    1 PRIOR-QUEUE +!
    OVER CELLS QUEUE-INDEX + PRIOR-QUEUE @ SWAP !
    PRIOR-QUEUE @ PRIOR-QUEUE-ITEM-ADDRESS 2!
    PRIOR-QUEUE @ PRIOR-QUEUE-SIFT-UP ;

: PRIOR-QUEUE-EXTRACT ( -- record, priority )
    ASSERT( PRIOR-QUEUE @ )
    1 PRIOR-QUEUE-ITEM-ADDRESS 2@
    1 PRIOR-QUEUE @ PRIOR-QUEUE-SWAP
    -1 PRIOR-QUEUE +!
    1 PRIOR-QUEUE-SIFT-DOWN ;
    
: PRIOR-QUEUE-UPDATE ( record,prority )
    SWAP CELLS QUEUE-INDEX + @
    DUP PRIOR-QUEUE-ITEM-ADDRESS CELL+ ROT SWAP !
    DUP PRIOR-QUEUE-SIFT-UP
    DUP PRIOR-QUEUE-SIFT-DOWN ;


